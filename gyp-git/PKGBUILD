# Maintainer: robertfoster

pkgname=gyp-git
pkgver=r2163.d6c5dd51
pkgrel=1
epoch=1
pkgdesc="GYP can Generate Your Projects."
url="http://code.google.com/p/gyp/"
arch=('i686' 'x86_64')
makedepends=('git')
depends=('python2' 'python2-setuptools')
license=('BSD')
source=("git+https://chromium.googlesource.com/external/gyp")
replaces=('gyp-svn')

pkgver() {
  cd "${srcdir}/gyp"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/gyp"
  python2 setup.py build
}

#check() {
#  cd "${srcdir}/gyp"
#  python2 gyptest.py -a
#}

package() {
  cd "${srcdir}/gyp"
  python2 setup.py install --skip-build --optimize=1 --prefix=/usr --root="${pkgdir}"
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

md5sums=('SKIP')
