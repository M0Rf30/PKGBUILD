# Maintainer: robertfoster

pkgname=ggml
pkgver=r2100.6757e32
pkgrel=1
pkgdesc="Tensor library for machine learning"
arch=('armv7h' 'aarch64' 'x86_64')
url="https://github.com/ggml-org/ggml"
license=("MIT")
depends=('openblas')
makedepends=(
  'cmake'
  'git'
)
conflicts=('libggml')
provides=('libggml')
_commit=6757e3217897ee033a0a3178203b5c4b0254d29e
source=("${pkgname}::git+${url}#commit=${_commit}")

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cmake \
    -B "${srcdir}/build" \
    -S "${srcdir}/${pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DGGML_BLAS=1 \
    -DGGML_BLAS_VENDOR=OpenBLAS

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${srcdir}/build"

  install -Dm644 "${srcdir}/${pkgname}/LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

sha256sums=('SKIP')
