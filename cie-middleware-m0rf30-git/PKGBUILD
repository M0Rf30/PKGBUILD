# Maintainer: robertfoster
# Contributor: FabioLolix

pkgname=cie-middleware-m0rf30-git
pkgver=1.4.1.r5.b2aba10
pkgrel=1
pkgdesc="Middleware della CIE (Carta di Identit√† Elettronica) per Linux, M0Rf30 fork with working build system"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://github.com/M0Rf30/cie-middleware-linux"
license=('BSD')
depends=('crypto++' 'openssl' 'pcsclite' 'java-runtime')
makedepends=('git' 'gradle' 'meson')
provides=(cie-middleware)
conflicts=(cie-middleware)
install="cie-middleware.install"

source=("${pkgname%-git}::git+https://github.com/M0Rf30/cie-middleware-linux")

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --tags --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  gradle -b cie-java/build.gradle standalone
  cd libcie-pkcs11/
  meson builddir
  meson configure -Dprefix=/usr builddir
  meson compile -C builddir
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  # Java Application
  install -Dm755 cie-java/bin/libs/CIEID-standalone.jar \
    "${pkgdir}/usr/share/cieid/cieid.jar"
  install -Dm644 "data/cieid.desktop" \
    "${pkgdir}/usr/share/applications/cieid.desktop"
  install -Dm755 data/logo.png \
    "${pkgdir}/usr/share/pixmaps/cieid.png"
  install -Dm755 "data/cieid.sh" \
    "${pkgdir}/usr/bin/cieid"
  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Lib for PKCS11
  DESTDIR="${pkgdir}" meson install -C libcie-pkcs11/builddir
}

md5sums=('SKIP')
