# Maintainer: robertfoster

pkgname=watermint-git
pkgver=2614.2ecf2c5
pkgrel=1
pkgdesc="A realistic version of Spearmint (ioquake3 fork) for future development with open-sourced realism-based first person shooters."
url="https://github.com/Watermint-Game"
license=('GPL')
arch=('i686' 'x86_64')
depends=('sdl' 'openal' 'freetype2' 'libvorbis' 'opus' 'opusfile' 'libogg' 'speex' 'zlib' 'libjpeg-turbo')
makedepends=('nasm' 'git')
conflicts=('watermint' 'watermint-icculus-svn' 'watermint-svn' 'iowatermint-svn')
provides=('watermint' 'iowatermint')
replaces=('watermint-icculus-svn' 'iowatermint-svn')
install=watermint-git.install
source=(
  'watermint::git+https://github.com/Watermint-Game/watermint-code.git'
  'watermint.desktop' 'watermint.launcher' 'watermintded.launcher' 'PAKSUMS'
  'http://ftp.gwdg.de/pub/misc/ftp.idsoftware.com/idstuff/quake3/linux/linuxq3apoint-1.32b-3.x86.run')

build() {
  cd watermint

  # Modify Makefile to correct install path
  sed -i "s:/usr/local/games/quake3:$pkgdir/opt/watermint:g" Makefile

  # Compile watermint
  make USE_INTERNAL_FREETYPE=0 USE_INTERNAL_FREETYPE=0 USE_INTERNAL_VORBIS=0 \
    USE_INTERNAL_OGG=0 USE_INTERNAL_SPEEX=0 USE_INTERNAL_ZLIB=0 \
    USE_INTERNAL_JPEG=0 JPDIR=/usr/include SPEEXDIR=/usr/include/speex \
    OGGDIR=/usr/include/ogg || return 1
}

package() {
  cd watermint

  # Install Files
  make copyfiles || return 1

  # Extract Patch Files
  cd $srcdir
  chmod +x $srcdir/linuxq3apoint-1.32b-3.x86.run
  $srcdir/linuxq3apoint-1.32b-3.x86.run --tar xf

  # Modify Launcher Scripts
  if [ "$CARCH" = "x86_64" ]; then
    #
    # x86_64 Systems
    #
    sed -i "s:WAT_BINARY:watermint.x86_64:" \
      $srcdir/watermint.launcher
    sed -i "s:WAT_BINARY:watermint-server.x86_64:" \
      $srcdir/watermintded.launcher
  else
    #
    # i686 Systems
    #
    sed -i "s:WAT_BINARY:watermint.i386:" \
      $srcdir/watermint.launcher
    sed -i "s:WAT_BINARY:watermint-server.i386:" \
      $srcdir/watermintded.launcher
  fi

  # Install Quake 3 Patch Files
  install -m 644 $srcdir/baseq3/*.pk3 $pkgdir/opt/watermint/mpbase/

  #    # Install Quake 3 Expansion Pack Patch Files
  #    install -m 644 $srcdir/missionpack/*.pk3 $pkgdir/opt/watermint/mpbase/

  # Install PAKSUMS
  install -m 644 $srcdir/PAKSUMS $pkgdir/opt/watermint/mpbase/

  # Install Launcher (Client)
  install -D -m 755 $srcdir/watermint.launcher $pkgdir/usr/bin/watermint

  # Install Launcher (Server)
  install -D -m 755 $srcdir/watermintded.launcher $pkgdir/usr/bin/watermintded

  # Install Desktop File
  install -D -m 644 $srcdir/watermint.desktop $pkgdir/usr/share/applications/watermint.desktop

  # Install Icon File
  install -D -m 644 $srcdir/watermint/misc/quake3-tango.png $pkgdir/usr/share/pixmaps/watermint.png
}

pkgver() {
  cd watermint
  echo $(git rev-list --count master).$(git rev-parse --short master)
}
sha256sums=('SKIP'
            'cce60913a4f93b8c2cd5a0a11e9ac5d1549b46e7d4302b9a15c91eb2230ee82a'
            '57e923b825ed9c251646d046f6761960eb428dd700765e318ee7999f1de88d0c'
            '57e923b825ed9c251646d046f6761960eb428dd700765e318ee7999f1de88d0c'
            'be75c042716fbbde7911cf51ba5cc4cca42e984ab10f7d2f357d93d48ea9cc19'
            'c36132c5556b35e01950f1e9c646235033a5130f87ad776ba2bc7becf4f4f186')
