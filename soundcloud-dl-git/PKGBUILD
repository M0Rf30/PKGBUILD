# Maintainer: robertfoster

pkgname=soundcloud-dl-git
pkgver=1.3.3b1.r163.g6dd6ef7
pkgrel=1
pkgdesc="Souncloud music downloader"
url="https://github.com/flyingrub/scdl"
arch=(any)
license=(GPL2)
depends=(
  'python-clint'
  'python-docopt'
  'python-mutagen'
  'python-simplejson'
  'python-termcolor'
  'python-requests'
)
makedepends=(git python-setuptools)
source=("${pkgname%-git}::git+https://github.com/flyingrub/scdl.git")

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  python setup.py install --skip-build --optimize=1 --prefix=/usr --root="${pkgdir}"
}

md5sums=('SKIP')
