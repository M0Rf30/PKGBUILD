# Maintainer: robertfoster

pkgname=ggml-sycl-f32
pkgver=r2100.6757e32
pkgrel=1
pkgdesc="Tensor library for machine learning (with Intel SYCL GPU optimizations and F32)"
arch=('armv7h' 'aarch64' 'x86_64')
url="https://github.com/ggml-org/ggml"
license=("MIT")
depends=('intel-oneapi-basekit')
makedepends=(
  'cmake'
  'git'
)
conflicts=('ggml' 'libggml')
provides=('ggml' 'libggml')
_commit=6757e3217897ee033a0a3178203b5c4b0254d29e
source=("${pkgname}::git+${url}#commit=${_commit}")

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  source /opt/intel/oneapi/setvars.sh
  cmake \
    -B "${srcdir}/build" \
    -S "${srcdir}/${pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_COMPILER=icx \
    -DCMAKE_CXX_COMPILER=icpx \
    -DGGML_SYCL=1

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${srcdir}/build"

  install -Dm644 "${srcdir}/${pkgname}/LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

sha256sums=('SKIP')
