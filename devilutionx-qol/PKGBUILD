# Maintainer: robertfoster
# Contributor: LIN Rs <LinRs[d]users.noreply.github.com>
# Contributor: yochananmarqos <mark.wagie at tutanota dot com>

_pkgname=devilutionX
pkgname=devilutionx-qol
pkgver=1.1.0
pkgrel=1
pkgdesc="Diablo devolved for linux (with Qualitity Of Life patchset by Manuel-K)"
arch=('armv6h' 'armv7h' 'arm' 'aarch64' 'i686' 'x86_64')
url="https://github.com/Manuel-K/devilutionX-QOL-patches"
license=('custom:unlicense')
depends=('graphite' 'libsodium' 'sdl2_mixer' 'sdl2_ttf')
makedepends=('cmake' 'git')
replaces=('devilutionx')
conflicts=('devilutionx')
install="$pkgname".install
options=('strip')
_qol_commit="7ae428b089e7bc463d72ece0e6047f28db092a3a"
source=("https://github.com/diasurgical/devilutionX/archive/$pkgver.tar.gz"
        "qol::git+https://github.com/Manuel-K/devilutionX-QOL-patches#commit=${_qol_commit}")

prepare() {
	cd "$srcdir/${_pkgname}-$pkgver"
	if [ ! -d build ]; then
		mkdir build
	fi
        patch -p1 < ../qol/infernity_common_v02.patch
	patch -p1 < ../qol/infernity_item_hightlight_v04.patch
	patch -p1 < ../qol/infernity_monster_hp_bar_v02.patch
	patch -p1 < ../qol/mk_lmb_clicker_01.patch
	patch -p1 < ../qol/mk_menu_label_02.patch
	patch -p1 < ../qol/mk_run_in_town_smooth_01.patch
	patch -p1 < ../qol/sergi4ua_play_sfx_on_level_up.patch
}

build() {
	cd "$srcdir/${_pkgname}-$pkgver/build"
	cmake .. \
		-DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
		-DPIE=ON \
		-DBINARY_RELEASE=ON \
		-DVERSION_NUM="$pkgver"

	cmake --build .
}

package() {
	cd "$srcdir/${_pkgname}-$pkgver/build"
	cmake --install .
}

md5sums=('76e7f5219e8f58ee71ab671b13ce3139'
         'SKIP')
